;;;; src/paredit.lisp -- structural editing, as pure functions.
;;;;
;;;; Every command here has the same shape: it takes the TEXT being edited (a
;;;; REPL's input, or the top-level form around an editor's caret) and the
;;;; caret's CHARACTER offset into it, and answers (values NEW-TEXT
;;;; NEW-OFFSET), or NIL when it declines.  Declining is ordinary -- an
;;;; unbalanced line, nothing of that shape under the caret -- and the front end
;;;; then does whatever the key would have done without paredit, which is how
;;;; `(' still types a paren when this cannot help.
;;;;
;;;; NIL rather than an error, and no view anywhere: that is what lets the whole
;;;; of this be tested on a machine with no window, and what keeps each front
;;;; end -- the Lisp Listener's AppKit and UIKit views, Heml's buffers -- down
;;;; to converting offsets and writing text back.
;;;;
;;;; A command may not signal.  A front end may call it from inside an
;;;; Objective-C method, where a condition would be swallowed and the key
;;;; seem to do nothing; RUN-PAREDIT-COMMAND turns one into declining.

(in-package #:sexp-edit)

;;; Balanced insertion ----------------------------------------------------------
;;;
;;; The part paredit is famous for, and the part revl had no need of: it edits
;;; whole files, where the parens are already balanced.  A listener's input
;;; region is a line being typed, so these are written for that.

(defun insert-pair (text offset)
  "( inserts () and leaves the caret between them.

Inside a string or a comment it declines, so a paren typed in prose stays one
paren."
  (if (code-position-p text offset)
      (values (concatenate 'string (subseq text 0 offset) "()" (subseq text offset))
              (1+ offset))
      nil))

(defun insert-quote (text offset)
  "\" inserts a pair of them.  Just before a string's closing quote -- the
one this inserted -- it steps over it, as ) steps over a close paren.  Anywhere
else in a string it goes in escaped, \\\".  In a comment it declines, and is
just a character.

The stepping over was missing, and so every string typed at the listener came
out wrong: the closing quote went in as a second one, \"hi\"\", and the parens
typed after it no longer met their partners."
  (cond ((code-position-p text offset)
         (values (concatenate 'string (subseq text 0 offset) "\"\""
                              (subseq text offset))
                 (1+ offset)))
        ;; The closing quote: in a string here, a quote next, and code again
        ;; just past it.  In a comment the far side is comment too.
        ((and (< offset (length text))
              (char= (char text offset) #\")
              (code-position-p text (1+ offset)))
         (values text (1+ offset)))
        ;; Anywhere else in a string, a quote goes in escaped, as Emacs's
        ;; paredit does it: a bare one would end the string there and leave
        ;; the rest of the line, and its parens, outside it.
        ((in-string-p text offset)
         (values (concatenate 'string (subseq text 0 offset) "\\\""
                              (subseq text offset))
                 (+ offset 2)))
        (t nil)))

(defun in-string-p (text offset)
  "True when OFFSET in TEXT lies inside a \"string\" -- after its opening
quote, so a quote typed there would end it."
  (multiple-value-bind (start end kind closed) (token-at text offset)
    (declare (ignore end closed))
    (and start (eq kind :string) (< start offset))))

(defun close-or-skip (text offset)
  ") steps over the close paren that is already there, rather than typing a
second one.  With none to step over it declines, and the toolkit types the
paren -- which is what an unclosed form wants."
  (when (and (code-position-p text offset)
             (< offset (length text))
             (char= (char text offset) #\)))
    (values text (1+ offset))))

(defun delete-paren-p (text position)
  "How a deletion should treat the parenthesis at POSITION: :MATCHED, which
means step over it, :UNMATCHED, which means let it go, or NIL when it is not a
parenthesis at all.

An UNMATCHED paren must be deletable.  It is the one that is wrong -- the line
is already unbalanced and deleting it is what fixes it -- so refusing there
leaves a character that cannot be removed except by clearing the line, which is
how the first version of this behaved and it was maddening."
  (and (< -1 position (length text))
       (member (char text position) '(#\( #\)))
       (code-position-p text position)
       (if (paren-match-offset text position) :matched :unmatched)))

(defun delete-empty-pair (text offset)
  "The two halves of the empty pair around OFFSET, deleted, or NIL."
  (let ((before (and (plusp offset) (char text (1- offset))))
        (after (and (< offset (length text)) (char text offset))))
    (when (and before after
               (or (and (char= before #\() (char= after #\)))
                   (and (char= before #\") (char= after #\"))))
      (values (concatenate 'string (subseq text 0 (1- offset))
                           (subseq text (1+ offset)))
              (1- offset)))))

(defun string-quote-at-p (text position)
  "True when the character at POSITION is a string's own quote, its opening
or its closing one -- not an escaped quote inside a string, nor one in a
comment."
  (and (< -1 position (length text))
       (char= (char text position) #\")
       (multiple-value-bind (start end kind closed) (token-at text position)
         (and start (eq kind :string)
              (or (= position start)
                  (and closed (= position (1- end))))))))

(defun delete-pair-backward (text offset)
  "Backspace: an empty pair goes whole; a MATCHED paren, or a string's own
quote, is stepped over rather than deleted -- after a closing paren the caret
goes inside the list, after an opening one out of it; anything else -- an
unmatched paren included -- is the toolkit's to delete.

Stepping over means answering the text with the caret one back, which the
front end takes as handled; declining means answering NIL, and the key does
what it always did."
  ;; MULTIPLE-VALUE-BIND, not OR: OR keeps only the first value, so the new
  ;; offset was silently dropped and the caret went to NIL.
  (when (plusp offset)
    (multiple-value-bind (new-text new-offset) (delete-empty-pair text offset)
      (cond (new-text (values new-text new-offset))
            ((or (eq :matched (delete-paren-p text (1- offset)))
                 (string-quote-at-p text (1- offset)))
             (values text (1- offset)))))))

(defun delete-pair-forward (text offset)
  "Forward delete, by the same rules as Backspace: an empty pair whole, a
matched paren or a string's quote stepped over -- before an opening paren the
caret goes into the list -- and an unmatched one deleted by the toolkit."
  (multiple-value-bind (new-text new-offset)
      (and (< offset (length text)) (delete-empty-pair text (1+ offset)))
    (cond (new-text (values new-text new-offset))
          ((or (eq :matched (delete-paren-p text offset))
               (string-quote-at-p text offset))
           (values text (1+ offset))))))

;;; Motion ----------------------------------------------------------------------

(defun forward-sexp (text offset)
  "Past the end of the next sexp.  The text is unchanged; only the caret moves."
  (multiple-value-bind (start end) (sexp-span-at text offset)
    (declare (ignore start))
    (when end (values text end))))

(defun backward-sexp (text offset)
  "To the start of the sexp before the caret."
  (let* ((spans (sexp-spans text 0))
         (previous (find-if (lambda (span) (< (car span) offset)) (reverse spans))))
    (cond ((null previous) nil)
          ;; Inside a form: its children are what to step through.
          ((> offset (cdr previous))
           (values text (car previous)))
          (t (let ((inner (remove-if-not (lambda (span) (< (car span) offset))
                                         (sexp-spans text (car previous)))))
               (values text (car (or (first (last inner)) previous))))))))

;;; The structural commands ------------------------------------------------------
;;;
;;; Thin names over APPLY-STRUCTURAL-EDIT, so that a keymap entry and a test
;;; name a command rather than an operation keyword.

(macrolet ((define-structural-command (name operation documentation)
             `(defun ,name (text offset)
                ,documentation
                (apply-structural-edit ,operation text offset))))
  (define-structural-command kill-sexp :kill
    "Delete the sexp at the caret.")
  (define-structural-command wrap-round :wrap
    "Wrap the form at the caret in a new pair of parens.")
  (define-structural-command splice :splice
    "Remove the parens around the form at the caret, keeping its contents.")
  (define-structural-command slurp-forward :slurp
    "Pull the next sexp in through this form's closing paren.")
  (define-structural-command barf-forward :barf
    "Push this form's last sexp out past its closing paren.")
  (define-structural-command slurp-backward :slurp-back
    "Pull the previous sexp in through this form's opening paren.")
  (define-structural-command barf-backward :barf-back
    "Push this form's first sexp out past its opening paren.")
  (define-structural-command raise-sexp :raise
    "Replace the enclosing form with the form at the caret.")
  (define-structural-command transpose-sexps :transpose
    "Swap the sexp at the caret with the one after it."))

(defparameter *paredit-commands*
  '(insert-pair insert-quote close-or-skip
    delete-pair-backward delete-pair-forward
    forward-sexp backward-sexp
    kill-sexp wrap-round splice slurp-forward barf-forward
    slurp-backward barf-backward raise-sexp transpose-sexps)
  "Every command a key may be bound to.  Checked when a binding is set, so a
misspelling is refused at the prompt rather than silently doing nothing.")

(defvar *command-error-function* nil
  "Called with a command's name and the condition, when a command signals:
the front end's place to note it.  The command then declines.")

(defun run-paredit-command (command text offset)
  "Run COMMAND, answering (values TEXT OFFSET) or NIL.  Never signals: a
command that breaks declines, and the key falls through to the toolkit."
  (when (and command (fboundp command))
    (handler-case (funcall command text offset)
      (error (condition)
        (when *command-error-function*
          (ignore-errors (funcall *command-error-function* command condition)))
        nil))))
